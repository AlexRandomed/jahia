###############################################################################
package org.jahia.modules.roles.rules

#list any import classes here.
import org.jahia.services.content.rules.*
import org.apache.log4j.Logger

expander rules.dsl

#declare any global variables here
global User user
global Service service
global Logger logger
global RoleService roleService
###############################################################################

rule "Initial permissions-to-role assignment"
	when
		A property j:assignedPermissions has been set on a node
			- the node has the type jnt:role
	then
		Log "Grant permissions " + propertyValue + " to role " + node.getPath()
		Grant a list of permissions propertyValue to role node.getPath()
		Remove this property
end

rule "Initial role-to-user assignment"
	when
		A property j:grantToPrincipals has been set on a node
			- the node has the type jnt:role
	then
		Log "Granting to principals " + propertyValue + " the role " + node.getPath()
		Grant role node.getPath() to principals propertyValue
		Remove this property
end

rule "Create permission for site languages"
	when
		A property j:languages has been set on a node
			- the node has the type jnt:virtualsite
	then
		Update site language permissions for node
		Grant permissions in group "languages" to role node.getPath() + "/roles/editor"
		Grant permissions in group "languages" to role node.getPath() + "/roles/editor-in-chief"
		Grant permissions in group "languages" to role node.getPath() + "/roles/publisher"
		Grant permissions in group "languages" to role node.getPath() + "/roles/seo-manager"
		Grant permissions in group "languages" to role node.getPath() + "/roles/site-administrator"
		Grant permissions in group "languages" to role node.getPath() + "/roles/translator"
		Grant permissions in group "languages" to role node.getPath() + "/roles/web-designer"
end

