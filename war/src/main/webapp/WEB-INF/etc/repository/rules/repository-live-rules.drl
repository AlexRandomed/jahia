#created on: 20 dec. 2007
package org.jahia.services.content.rules

#list any import classes here.
import org.jahia.services.content.rules.*
import org.jahia.services.content.*
import javax.jcr.observation.Event
import org.apache.log4j.Logger

expander rules.dsl

#declare any global variables here
global User user
global Service service
global ImageService imageService
global ExtractionService extractionService
global Logger logger
global JCRStoreProvider provider

rule "Metadata update / content created"
    salience -10
    no-loop
    when
        A new node is created
            - the node has the type mix:lastModified
            - the node has the type jmix:originWS
        The node has a parent
    then
      Set the property j:originWS of the node with the value "live"
      Set the property jcr:lastModifiedBy of the parent with the name of the current user
      Set the property jcr:lastModified of the parent with the current time
end

rule "Metadata update / property modified"
    salience -11
    no-loop
    when
        A property has been set on a node
            - the node has the type mix:lastModified
        The property jcr:lastModified has not been modified yet on the node
    then
        Set the property jcr:lastModified of the node with the current time
        Set the property jcr:lastModifiedBy of the node with the name of the current user
end

rule "Metadata update / property removed"
    salience -11
    no-loop
    when
        A property has been removed from a node
             - the node has the type mix:lastModified
        The property jcr:lastModified has not been modified yet on the node
    then
        Set the property jcr:lastModified of the node with the current time
        Set the property jcr:lastModifiedBy of the node with the name of the current user
end

rule "Metadata update / node deleted"
    salience -12
    no-loop
    when
        A node is deleted
        The node has a parent
             - the parent has the type mix:lastModified
    then
        Set the property jcr:lastModified of the parent with the current time
        Set the property jcr:lastModifiedBy of the parent with the name of the current user
end

