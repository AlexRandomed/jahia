package org.jahia.services.content.rules

#list any import classes here.
import org.jahia.services.content.rules.*
import org.jahia.services.content.*
import javax.jcr.observation.Event
import org.apache.log4j.Logger

expander rules.dsl

#declare any global variables here
global User user
global Service service
global ImageService imageService
global ExtractionService extractionService
global RulesNotificationService notificationService
global Logger logger
global JCRStoreProvider provider

rule "Test type"
    when
        A new node is created
        - it is in /shared/test
    then
        Add the type jmix:test
end

rule "publish user (create)"
    when
       A new node is created
		- the node has the type jnt:user
		- its name is not guest
		- its name is not root
    then
        Publish the node
end

rule "publish groups"
    when
       A new node is created
		- the node has the type jnt:group
    then
        Publish the node
end


// These 2 rules show how to take some actions based on the user properties

/*
rule "Assign acl to all my groups"
    when
        A new node is created
        The current user belongs to a group
    then
        Log "Assign acl to my group "+groupName
        Assign permissions "rw" on the node to this group
end
*/

/*
rule "Assign acl to my service"
    when
        A new node is created
        The current user has a property named service
    then
        Log "Assign acl to my service " +userProperty.getValue()
        Assign permissions "rw" on the node to a group matching that property
end
*/

// This is an example that shows how to propagate a property to all sub folders and sub files
// First rule copy the value from the parent when a file is uploaded / modified
// Second rule propagates a property change to all children

rule "Copy status parent value by new type"
    salience 25
    when
        The type jmix:categorizedDocument has been assigned to a node
        The property j:status is not defined for the node
        The node has a parent
        The parent has a property j:status
    then
        Log "Copy status parent value by new type"
        Set the property j:status of the node with the value of that property
end

rule "Propagate status value to children"
    salience 25
    when
        A property j:status has been set on a node
        The node has a child
            - it has the extension type jmix:categorizedDocument
    then
        Log "Propagate status value to child "+child
        Set the property j:status of the child with the value of that property
end

rule "Import file"
    salience 50
    when
        A new node is created
        The node has a parent
             - it has the extension type jnt:importDropBox
    then
        Import the node
        Log "Import file fired for node " + node.getPath()
end

rule "Image type"
#Assign jmix:image type for an image node
    salience 50
    no-loop
    when
        A new node is created
             - the mimetype matches image/.*
    then
        Log "Assign image type (jmix:image) for node " + node.getPath()
        Add the type jmix:image
end

rule "Image update"
    salience 25
#Rebuild thumbnail for an updated image and update height/width
    when
        A file content has been modified
             - the mimetype matches image/.*
    then
        Create an image "thumbnail" of size 150
        Create an image "thumbnail2" of size 350
        Set the property j:width of the node with the width of the image
        Set the property j:height of the node with the height of the image
        Log "Image updated " + node.getPath()
end

rule "Profile Image update"
    salience 25
    when
        A property j:picture has been set on a node
             - the node has the type jnt:user
    then
        Create a square thumbnail on reference "avatar_120" of size 120
        Create a square thumbnail on reference "avatar_60" of size 60
end

rule "Move subnodes to split folder"
    salience 100
    when
        The type jmix:autoSplitFolders has been assigned to a node
    then
        Move subnodes of node to split folder
end

rule "Move to split folder"
    salience 100
    when
        A new node is created
            - the parent has the type jmix:autoSplitFolders
    then
        Move to split folder node
end

# Portlet rules
rule "Portlet definition deleted"
    when
        A node is deleted
        The node has a parent
            - the parent has the type jnt:portletDefinitions
    then
        Flush ApplicationCache
        Flush ApplicationContextCache
end

rule "notify user (create)"
    when
       A new node is created
		- the node has the type jnt:user
		- its name is not guest
		- its name is not root
    then
        Notify new user with mail template "/notifications/templates/mail/newUser.vm"
end